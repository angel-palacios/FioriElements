@Metadata.layer: #CORE
@UI: {
    headerInfo: {
        typeName: 'Travel',
        typeNamePlural: 'Travels',
        title: {
            type: #STANDARD,
            value: 'Description'
        },
        description: {
            type: #STANDARD,
            value: 'TravelID' }
    },
    presentationVariant: [{
        sortOrder: [{
            by: 'LocalLastChangedAt',
            direction: #DESC
        }],
        visualizations: [{
            type: #AS_LINEITEM
        }]
    }]
}
annotate view ZC_FE_Travel_002039 with
{
  /* Header Facets */
  @UI.facet: [
    {
    purpose: #HEADER,
    position: 10,
    type: #DATAPOINT_REFERENCE,
    targetQualifier: 'PriceData'
    },
    {
    purpose: #HEADER,
    position: 20,
    type: #DATAPOINT_REFERENCE,
    targetQualifier: 'StatusData'
    },
    /*Standard Facets*/
    //Section 'General Information'
    {
    label: 'General Information',
    id: 'GeneralInfo',
    type: #COLLECTION,
    position: 10
    },
    //field group 'General', which contains all the identification fields
    {
    label: 'General',
    purpose: #STANDARD,
    position: 10,
    type: #IDENTIFICATION_REFERENCE,
    parentId: 'GeneralInfo'
    },
    //Field groups Prices and Dates
    {
    label: 'Prices',
    purpose: #STANDARD,
    position: 20,
    type: #FIELDGROUP_REFERENCE,
    parentId: 'GeneralInfo',
    targetQualifier: 'PricesGroup'
    },
    {
    label: 'Dates',
    purpose: #STANDARD,
    position: 30,
    type: #FIELDGROUP_REFERENCE,
    parentId: 'GeneralInfo',
    targetQualifier: 'DatesGroup'
    },
    // section Bookings
    {  
    id: 'Bookings',
    label: 'Bookings',
    purpose: #STANDARD,
    type: #LINEITEM_REFERENCE,
    position: 20,
    targetElement: '_Booking'
    }
   ]

  @UI.identification: [{ position: 10 }]
  Description;

  @UI.lineItem: [{ position: 10,
                   importance: #HIGH
                }]
  TravelID;
  @UI.lineItem: [{ position: 20,
                   importance: #HIGH
                 }]
  @UI.selectionField: [{ position: 10 }]
  @UI.identification: [{ position: 20 }]
  AgencyID;

  @UI.lineItem: [{ position: 30,
                   importance: #HIGH
                 }]
  @UI.selectionField: [{ position: 20 }]
  @UI.identification: [{ position: 20 }]
  CustomerID;

  @UI.lineItem: [{ position: 40,
                   importance: #HIGH
                 }]
  @UI.fieldGroup: [{ qualifier: 'DatesGroup',
                     position: 10
                  }]
  BeginDate;

  @UI.lineItem: [{ position: 50,
                   importance: #HIGH
                 }]
  @UI.fieldGroup: [{ qualifier: 'DatesGroup',
                     position: 20
                  }]
  EndDate;

  @UI.lineItem: [{ position: 60,
                   importance: #HIGH
                 }]
  @UI.fieldGroup: [{ qualifier: 'PricesGroup',
                     position: 10
                  }]
  BookingFee;

  @UI.lineItem: [{ position: 70,
                   importance: #HIGH
                 }]
  @UI.dataPoint: { qualifier: 'PriceData', title: 'Total Price' }
  @UI.fieldGroup: [{ qualifier: 'PricesGroup',
                     position: 20
                  }]
  TotalPrice;

  @UI.lineItem: [{ position: 80,
                   importance: #HIGH,
                   criticality: 'OverallStatusCriticality' }]
  @UI.selectionField: [{ position: 30 }]
  @UI.textArrangement: #TEXT_ONLY
  @UI.dataPoint: { qualifier: 'StatusData',
                   title: 'Status',
                   criticality: 'OverallStatusCriticality' }
  OverallStatus;

  @UI.lineItem: [{ position: 90,
                   importance: #HIGH
                 }]
  LocalLastChangedAt;

  @UI.hidden: true
  OverallStatusCriticality;

}